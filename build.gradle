plugins {
	id 'com.github.johnrengelman.shadow' version '7.0.0' apply false
}

subprojects {

	apply plugin: "java"
	apply plugin: "maven-publish"
	apply plugin: "com.github.johnrengelman.shadow"

	repositories {
		mavenLocal()
		maven { url "https://repo.astrocube.net/repository/maven-snapshots/" }
		maven { url "https://maven.timo.cloud/repository/TimoCloud-API/" }
		maven { url "https://repo.codemc.io/repository/maven-snapshots/" }
		maven { url "https://repo.unnamed.team/repository/unnamed-snapshots/" }
		mavenCentral()

		["astro-releases", "astro-snapshots", "core-releases", "core-snapshots", "maven-releases"].forEach { repo ->
			maven {
				url = "https://repo.astrocube.net/repository/$repo/"
				credentials {
					username = project.properties["ASTRO_REPO_USER"]
					password = project.properties["ASTRO_REPO_PWD"]
				}
			}
		}
	}

	compileJava {
		options.compilerArgs << "-parameters"
	}

	shadowJar {
		archiveFileName = "${project.name}.jar"
	}

	jar {
		archiveFileName = "original-${project.name}.jar"
	}

	dependencies {
		compileOnly "org.projectlombok:lombok:$lombokVersion"
		annotationProcessor "org.projectlombok:lombok:$lombokVersion"
	}

}